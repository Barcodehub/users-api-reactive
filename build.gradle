plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.6'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.4")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'io.github.resilience4j:resilience4j-spring-boot2:2.2.0'
	implementation 'io.github.resilience4j:resilience4j-reactor:2.2.0'
	implementation 'io.github.resilience4j:resilience4j-retry'
	implementation 'io.github.resilience4j:resilience4j-bulkhead'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.postgresql:r2dbc-postgresql'
	implementation 'org.mapstruct:mapstruct:1.6.0'

	// Swagger/OpenAPI
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.3.0'

	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, include: [
				'**/domain/**'
			], exclude: [
				'**/infrastructure/**',
				'**/application/**'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.90
			}
			includes = ['com.example.resilient_api.domain.*']
			excludes = [
				'com.example.resilient_api.infrastructure.*',
				'com.example.resilient_api.application.*'
			]
		}
	}
}

check.dependsOn jacocoTestCoverageVerification
